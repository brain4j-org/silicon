plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.3.0'
}

group = 'org.silicon'
version = '1.0.0-SNAPSHOT.1'

subprojects {
    version = rootProject.version
    group = rootProject.group

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.gradleup.shadow'

    repositories {
        mavenCentral()
        maven {
            name "brain4jReleases"
            url "https://repo.brain4j.org/releases"
        }
    }

    shadowJar {
        archiveClassifier.set("")
    }

    publishing {
        repositories {
            maven {
                name = "silicon"

                def releasesRepo = uri("https://repo.brain4j.org/releases")
                def snapshotsRepo = uri("https://repo.brain4j.org/snapshots")

                url = version.endsWith("SNAPSHOT") ? snapshotsRepo : releasesRepo

                credentials {
                    username = findProperty("BRAIN4J_USERNAME")
                    password = findProperty("BRAIN4J_TOKEN")
                }

                authentication {
                    basic(BasicAuthentication)
                }
            }
        }

        publications {
            gpr(MavenPublication) {
                from components.java
                groupId = project.group
                artifactId = project.name
                version = project.version
            }
        }
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(21))
        }

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

tasks.register('buildAll') {
    dependsOn ':silicon-api:shadowJar'
    dependsOn ':silicon-cuda:shadowJar'
    dependsOn ':silicon-metal:shadowJar'
    dependsOn ':silicon-opencl:shadowJar'

    doLast {
        println 'Built all modules.'
    }
}

tasks.register('publishAll') {
    dependsOn ':silicon-api:publish'
    dependsOn ':silicon-cuda:publish'
    dependsOn ':silicon-metal:publish'
    dependsOn ':silicon-opencl:publish'

    doLast {
        println 'Published all modules to the main repository.'
    }
}